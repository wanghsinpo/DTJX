<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML 2.0//EN">
<HTML><HEAD>
<A NAME="Widget_itk_3_0"></A>
<TITLE>Widget [incr\ Tk] 3.0</TITLE></HEAD>
<BODY>
<HR>
<P>
<H2>NAME</H2>
 Widget - base class for mega-widgets within a frame <P>
<H2>INHERITANCE</H2>
 itk::Archetype &lt;- itk::Widget <P>
<H2>STANDARD OPTIONS</H2>
 <P>

<B>background</B>	<B>cursor</B>
 <P>
See the <a href="../mann/options.html">options</a> manual entry for details on the standard options. <HR>
<P>
<P>
<H2>DESCRIPTION</H2>
 <P>
The <B>Widget</B> class inherits everything from the <B>Archetype</B> class, and adds a Tk frame called the "hull" component to represent the body of the mega-widget.  The window class name for the hull is set to the most-specific class name for the mega-widget. The protected variable <B>itk_interior</B> contains the window path name for the "hull" component.  Derived classes specialize this widget by packing other widget components into the hull. <P>
Since the hull for the <B>Widget</B> class is implemented with a Tk frame, mega-widgets in the <B>Widget</B> class can be packed into other frames and toplevels. <P>
<P>
<H2>COMPONENTS</H2>
 <P>

Name:	<B>hull</B>
Class:	<B>Frame</B>
 
<DD> The "hull" component acts as the body for the entire mega-widget. Other components are packed into the hull to further specialize the widget. <P>
<P>
<H2>EXAMPLE</H2>
 <P>
The following example implements a simple <B>TextDisplay</B> mega-widget.  It creates a read-only display of text with a text widget and a scrollbar. <PRE>
option add *TextDisplay.wrap none widgetDefault
option add *TextDisplay.textBackground ivory widgetDefault
option add *TextDisplay.width 40 widgetDefault
option add *TextDisplay.height 10 widgetDefault
<P>
class TextDisplay {
    inherit itk::Widget
<P>
    constructor {args} {
        itk_component add text {
            text $itk_interior.info -state disabled \
                -yscrollcommand [code $itk_interior.sbar set]
        } {
            usual
            keep -tabs -wrap -width -height
            rename -background -textbackground textBackground Background
        }
        pack $itk_component(text) -side left -expand yes -fill both
<P>
        itk_component add scrollbar {
            scrollbar $itk_interior.sbar \
                -command [code $itk_interior.info yview]
        }
        pack $itk_component(scrollbar) -side right -fill y
<P>
        eval itk_initialize $args
    }
<P>
    public method display {info}
    public method append {info}
}
<P>
body TextDisplay::display {info} {
    $itk_component(text) configure -state normal
    $itk_component(text) delete 1.0 end
    $itk_component(text) insert 1.0 $info
    $itk_component(text) configure -state disabled
}
<P>
body TextDisplay::append {info} {
    $itk_component(text) configure -state normal
    $itk_component(text) insert end $info
    $itk_component(text) configure -state disabled
}
<P>
usual TextDisplay {
    keep -background -cursor -foreground -font
    keep -activebackground -activerelief
    keep -highlightcolor -highlightthickness
    keep -insertbackground -insertborderwidth -insertwidth
    keep -insertontime -insertofftime
    keep -selectbackground -selectborderwidth -selectforeground
    keep -textbackground -troughcolor
}
<P>
#
# EXAMPLE:  Display the /etc/passwd file
#
TextDisplay .file -background red
pack .file
<P>
.file display [exec cat /etc/passwd]
</PRE>
<P>
<P>
<H2>KEYWORDS</H2>
 itk, Archetype, Widget, mega-widget 
</BODY>
</HTML>
